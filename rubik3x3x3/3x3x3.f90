MODULE RUBIK3

   INTEGER(4),PARAMETER :: N=1000000
   INTEGER(4) :: LIST(1:48,1:N),SUBLIST(1:48,1:N),NEWLIST(1:48,1:N)
   INTEGER(4) :: NEWIDENTITY(1:N),SUBIDENTITY(1:N)
   INTEGER(4) :: ILIST,ISUB,INEW

END MODULE



PROGRAM RUBIK_3X3X3
! BLANKET ENUMERATION OF 3X3X3 RUBIK'S CUBE CONFIGURATIONS.
! SO HIRATA, UNIVERSITY OF ILLINOIS AT URBANA-CHAMPAIGN, JANUARY 2024.

   USE RUBIK3

   IMPLICIT NONE

   INTERFACE
      INTEGER FUNCTION IEXIST(CONFIG)
         INTEGER(4) :: CONFIG(1:48)
      END FUNCTION
   END INTERFACE

   INTEGER(4)  :: I,J,K
   REAL :: START,FINISH,S2,F2
   INTEGER(4),PARAMETER :: MAXSTEPS=50
   INTEGER(4),PARAMETER :: ALGORITHM=2 ! 1 SORTED; 2 UNSORTED
   INTEGER(4) :: METRIC
   INTEGER(4) :: ICUBOID(1:48),JCUBOID(1:48),KCUBOID(1:48)
   INTEGER(4) :: ISTEP,JFOUND,ITURN,NTURN

   WRITE(6,'(A)') "3X3X3 RUBIK'S CUBE CONFIGURATION GENERATION"
   WRITE(6,'(A)') '1: QUARTER-TURN METRIC; 2: HALF-TURN METRIC; 3: SQUARE-SLICE SUBGROUP; 4: SQUARE SUBGROUP'
   READ(5,*) METRIC
   IF (METRIC==1) THEN
    WRITE(6,'(A)') 'QUARTER-TURN METRIC'
   ELSE IF (METRIC==2) THEN
    WRITE(6,'(A)') 'HALF-TURN METRIC'
   ELSE IF (METRIC==3) THEN
    WRITE(6,'(A)') 'SQUARE-SLICE SUBGROUP'
   ELSE IF (METRIC==4) THEN
    WRITE(6,'(A)') 'SQUARE SUBGROUP'
   ELSE
    WRITE(6,'(A)') 'ILLEGAL METRIC'
    STOP
   ENDIF

   WRITE(6,'(A,I0)') 'MAX NUMBER OF CONFIGURATIONS = ',N

   LIST=0
   SUBLIST=0

   ! INITIAL CONFIGURATION
   ISTEP=0
   CALL INITIALIZE(ICUBOID)
!  WRITE(6,'(48I1)') (ICUBOID(J),J=1,48)
   LIST(:,1)=ICUBOID(:)
   ILIST=1
   SUBLIST(:,1)=ICUBOID(:)
   SUBIDENTITY(1)=1
   ISUB=1
   INEW=0
   WRITE(6,'(3(A,I10))') 'STEP ',ISTEP,' NEW CONFIGS ',1,' CUMMULATIVE CONFIGS ',ILIST

   DO ISTEP=1,MAXSTEPS
    CALL CPU_TIME(START)
    CALL CPU_TIME(S2)
    NEWLIST=0
    NEWIDENTITY=0
    INEW=0
    DO I=1,ISUB
     IF (ISUB > 100000) THEN
      IF (MOD(I,10000)==0) THEN
       CALL CPU_TIME(F2)
       WRITE(6,'(A,I8,A,F20.3)') '    SEED CONFIGS ',I,' CPU TIME ',F2-S2
       CALL CPU_TIME(S2)
      ENDIF
     ENDIF
     ICUBOID(:)=SUBLIST(:,I)
!    QUARTER-TURN METRIC
     IF (METRIC==1) THEN
      NTURN=12
      DO ITURN=1,NTURN
       IF (ITURN== 1) CALL R(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 2) CALL R(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 3) CALL D(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 4) CALL D(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 5) CALL B(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 6) CALL B(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 7) CALL L(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 8) CALL L(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 9) CALL U(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN==10) CALL U(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN==11) CALL F(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN==12) CALL F(ICUBOID,JCUBOID,.FALSE.)
       JFOUND=IEXIST(JCUBOID)
       IF (JFOUND==0) THEN
        ILIST=ILIST+1
        IF (ILIST > N) THEN
         WRITE(6,'(A)') 'ERROR 2'
         STOP
        ENDIF
        LIST(:,ILIST)=JCUBOID(:)
        INEW=INEW+1
        IF (INEW > N) THEN
         WRITE(6,'(A)') 'ERROR 3'
         STOP
        ENDIF
        NEWLIST(:,INEW)=JCUBOID(:)
        NEWIDENTITY(INEW)=ILIST
if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',ILIST,','
       ELSE
if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',JFOUND,','
       ENDIF
      ENDDO
!    HALF-TURN METRIC
     ELSE IF (METRIC==2) THEN
      NTURN=18
      DO ITURN=1,NTURN
       IF (ITURN== 1) CALL R(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 2) CALL R(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 3) CALL D(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 4) CALL D(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 5) CALL B(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 6) CALL B(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 7) CALL L(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN== 8) CALL L(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN== 9) CALL U(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN==10) CALL U(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN==11) CALL F(ICUBOID,JCUBOID,.TRUE.)
       IF (ITURN==12) CALL F(ICUBOID,JCUBOID,.FALSE.)
       IF (ITURN==13) THEN
        CALL R(ICUBOID,KCUBOID,.TRUE.)
        CALL R(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==14) THEN
        CALL D(ICUBOID,KCUBOID,.TRUE.)
        CALL D(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==15) THEN
        CALL B(ICUBOID,KCUBOID,.TRUE.)
        CALL B(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==16) THEN
        CALL L(ICUBOID,KCUBOID,.TRUE.)
        CALL L(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==17) THEN
        CALL U(ICUBOID,KCUBOID,.TRUE.)
        CALL U(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==18) THEN
        CALL F(ICUBOID,KCUBOID,.TRUE.)
        CALL F(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       JFOUND=IEXIST(JCUBOID)
       IF (JFOUND==0) THEN
        ILIST=ILIST+1
        IF (ILIST > N) THEN
         WRITE(6,'(A)') 'ERROR 5'
         STOP
        ENDIF
        LIST(:,ILIST)=JCUBOID(:)
        INEW=INEW+1
        IF (INEW > N) THEN
         WRITE(6,'(A)') 'ERROR 6'
         STOP
        ENDIF
        NEWLIST(:,INEW)=JCUBOID(:)
        NEWIDENTITY(INEW)=ILIST
if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',ILIST,','
       ELSE
if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',JFOUND,','
       ENDIF 
      ENDDO
!    SQUARE-SLICE SUBGROUP
     ELSE IF (METRIC==3) THEN
      NTURN=3
      DO ITURN=1,NTURN
       IF (ITURN==1) THEN
        CALL R(ICUBOID,KCUBOID,.TRUE.)
        CALL R(KCUBOID,JCUBOID,.TRUE.)
        CALL L(JCUBOID,KCUBOID,.FALSE.)
        CALL L(KCUBOID,JCUBOID,.FALSE.)
       ENDIF
       IF (ITURN==2) THEN
        CALL D(ICUBOID,KCUBOID,.TRUE.)
        CALL D(KCUBOID,JCUBOID,.TRUE.)
        CALL U(JCUBOID,KCUBOID,.FALSE.)
        CALL U(KCUBOID,JCUBOID,.FALSE.)
       ENDIF
       IF (ITURN==3) THEN
        CALL F(ICUBOID,KCUBOID,.TRUE.)
        CALL F(KCUBOID,JCUBOID,.TRUE.)
        CALL B(JCUBOID,KCUBOID,.FALSE.)
        CALL B(KCUBOID,JCUBOID,.FALSE.)
       ENDIF
       JFOUND=IEXIST(JCUBOID)
       IF (JFOUND==0) THEN
        ILIST=ILIST+1
        IF (ILIST > N) THEN
         WRITE(6,'(A)') 'ERROR 5'
         STOP
        ENDIF
        LIST(:,ILIST)=JCUBOID(:)
        INEW=INEW+1
        IF (INEW > N) THEN
         WRITE(6,'(A)') 'ERROR 6'
         STOP
        ENDIF
        NEWLIST(:,INEW)=JCUBOID(:)
        NEWIDENTITY(INEW)=ILIST
if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',ILIST,','
       ELSE
if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',JFOUND,','
       ENDIF 
      ENDDO
!    SQUARE SUBGROUP
     ELSE IF (METRIC==4) THEN
      NTURN=6
      DO ITURN=1,NTURN
       IF (ITURN==1) THEN
        CALL R(ICUBOID,KCUBOID,.TRUE.)
        CALL R(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==2) THEN
        CALL L(ICUBOID,KCUBOID,.FALSE.)
        CALL L(KCUBOID,JCUBOID,.FALSE.)
       ENDIF
       IF (ITURN==3) THEN
        CALL D(ICUBOID,KCUBOID,.TRUE.)
        CALL D(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==4) THEN
        CALL U(ICUBOID,KCUBOID,.FALSE.)
        CALL U(KCUBOID,JCUBOID,.FALSE.)
       ENDIF
       IF (ITURN==5) THEN
        CALL F(ICUBOID,KCUBOID,.TRUE.)
        CALL F(KCUBOID,JCUBOID,.TRUE.)
       ENDIF
       IF (ITURN==6) THEN
        CALL B(ICUBOID,KCUBOID,.FALSE.)
        CALL B(KCUBOID,JCUBOID,.FALSE.)
       ENDIF
       JFOUND=IEXIST(JCUBOID)
       IF (JFOUND==0) THEN
        ILIST=ILIST+1
        IF (ILIST > N) THEN
         WRITE(6,'(A)') 'ERROR 7'
         STOP
        ENDIF
        LIST(:,ILIST)=JCUBOID(:)
        INEW=INEW+1
        IF (INEW > N) THEN
         WRITE(6,'(A)') 'ERROR 8'
         STOP
        ENDIF
        NEWLIST(:,INEW)=JCUBOID(:)
        NEWIDENTITY(INEW)=ILIST
!if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',ILIST,','
       ELSE
!if (istep <= 3) write(*,'(I4,A,I4,A)') SUBIDENTITY(I),'<->',JFOUND,','
       ENDIF 
      ENDDO
     ENDIF
    ENDDO
    IF (INEW==0) THEN
     IF (ILIST==N) THEN
      EXIT
     ELSE
      WRITE(6,'(A,I0)') 'ILLEGAL TERMINATION WITH CUMMULATIVE CONFIGURATIONS ',ILIST
      EXIT
     ENDIF
    ENDIF
    CALL CPU_TIME(FINISH)
    WRITE(6,'(3(A,I10),A,F20.3)') 'STEP ',ISTEP,' NEW CONFIGS ',INEW,' CUMMULATIVE CONFIGS ',ILIST,' CPU TIME ',FINISH-START
    ISUB=INEW
    SUBLIST=NEWLIST
    SUBIDENTITY=NEWIDENTITY
   ENDDO
   CALL CPU_TIME(FINISH)

END PROGRAM



SUBROUTINE INITIALIZE(ICUBOID)
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!

   IMPLICIT NONE
   INTEGER(4)  :: I,J
   INTEGER(4)  :: ICUBOID(1:48)

   DO I=1,6
    DO J=(I-1)*8+1,I*8
     ICUBOID(J)=I
    ENDDO
   ENDDO
   RETURN

END SUBROUTINE



SUBROUTINE R(ICUBOID,JCUBOID,LPRIME)
! PERFORM RIGHT LAYER ROTATE BY 90 DEGREES
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!
! 3->11;4->12;5->13;11->47;12->48;13->41;41->37;48->36;47->35;35->3;36->4;37->5; 
! 25->27;26->28;27->29;28->30;29->31;30->32;31->25;32->26

   IMPLICIT NONE
   INTEGER(4)  :: ICUBOID(1:48)
   INTEGER(4)  :: JCUBOID(1:48)
   LOGICAL     :: LPRIME ! TRUE CLOCKWISE; FALSE COUNTERCLOCKWISE

   JCUBOID=ICUBOID

   IF (LPRIME) THEN
    JCUBOID(11)=ICUBOID( 3)
    JCUBOID(12)=ICUBOID( 4)
    JCUBOID(13)=ICUBOID( 5)
    JCUBOID(47)=ICUBOID(11)
    JCUBOID(48)=ICUBOID(12)
    JCUBOID(41)=ICUBOID(13)
    JCUBOID(37)=ICUBOID(41)
    JCUBOID(36)=ICUBOID(48)
    JCUBOID(35)=ICUBOID(47)
    JCUBOID( 3)=ICUBOID(35)
    JCUBOID( 4)=ICUBOID(36)
    JCUBOID( 5)=ICUBOID(37)
    JCUBOID(27)=ICUBOID(25)
    JCUBOID(28)=ICUBOID(26)
    JCUBOID(29)=ICUBOID(27)
    JCUBOID(30)=ICUBOID(28)
    JCUBOID(31)=ICUBOID(29)
    JCUBOID(32)=ICUBOID(30)
    JCUBOID(25)=ICUBOID(31)
    JCUBOID(26)=ICUBOID(32)
   ELSE
    JCUBOID( 3)=ICUBOID(11)
    JCUBOID( 4)=ICUBOID(12)
    JCUBOID( 5)=ICUBOID(13)
    JCUBOID(11)=ICUBOID(47)
    JCUBOID(12)=ICUBOID(48)
    JCUBOID(13)=ICUBOID(41)
    JCUBOID(41)=ICUBOID(37)
    JCUBOID(48)=ICUBOID(36)
    JCUBOID(47)=ICUBOID(35)
    JCUBOID(35)=ICUBOID( 3)
    JCUBOID(36)=ICUBOID( 4)
    JCUBOID(37)=ICUBOID( 5)
    JCUBOID(25)=ICUBOID(27)
    JCUBOID(26)=ICUBOID(28)
    JCUBOID(27)=ICUBOID(29)
    JCUBOID(28)=ICUBOID(30)
    JCUBOID(29)=ICUBOID(31)
    JCUBOID(30)=ICUBOID(32)
    JCUBOID(31)=ICUBOID(25)
    JCUBOID(32)=ICUBOID(26)
   ENDIF
   RETURN

END SUBROUTINE



SUBROUTINE L(ICUBOID,JCUBOID,LPRIME)
! PERFORM LEFT LAYER ROTATE BY 90 DEGREES
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!
! 1->33;8->40;7->39;33->45;40->44;39->43;45->9;44->16;43->15;9->1;16->8;15->7
! 17->19;18->20;19->21;20->22;21->23;22->24;23->17;24->18

   IMPLICIT NONE
   INTEGER(4)  :: ICUBOID(1:48)
   INTEGER(4)  :: JCUBOID(1:48)
   LOGICAL     :: LPRIME ! TRUE CLOCKWISE; FALSE COUNTERCLOCKWISE

   JCUBOID=ICUBOID

   IF (LPRIME) THEN
    JCUBOID(33)=ICUBOID( 1)
    JCUBOID(40)=ICUBOID( 8)
    JCUBOID(39)=ICUBOID( 7)
    JCUBOID(45)=ICUBOID(33)
    JCUBOID(44)=ICUBOID(40)
    JCUBOID(43)=ICUBOID(39)
    JCUBOID( 9)=ICUBOID(45)
    JCUBOID(16)=ICUBOID(44)
    JCUBOID(15)=ICUBOID(43)
    JCUBOID( 1)=ICUBOID( 9)
    JCUBOID( 8)=ICUBOID(16)
    JCUBOID( 7)=ICUBOID(15)
    JCUBOID(19)=ICUBOID(17)
    JCUBOID(20)=ICUBOID(18)
    JCUBOID(21)=ICUBOID(19)
    JCUBOID(22)=ICUBOID(20)
    JCUBOID(23)=ICUBOID(21)
    JCUBOID(24)=ICUBOID(22)
    JCUBOID(17)=ICUBOID(23)
    JCUBOID(18)=ICUBOID(24)
   ELSE
    JCUBOID( 1)=ICUBOID(33)
    JCUBOID( 8)=ICUBOID(40)
    JCUBOID( 7)=ICUBOID(39)
    JCUBOID(33)=ICUBOID(45)
    JCUBOID(40)=ICUBOID(44)
    JCUBOID(39)=ICUBOID(43)
    JCUBOID(45)=ICUBOID( 9)
    JCUBOID(44)=ICUBOID(16)
    JCUBOID(43)=ICUBOID(15)
    JCUBOID( 9)=ICUBOID( 1)
    JCUBOID(16)=ICUBOID( 8)
    JCUBOID(15)=ICUBOID( 7)
    JCUBOID(17)=ICUBOID(19)
    JCUBOID(18)=ICUBOID(20)
    JCUBOID(19)=ICUBOID(21)
    JCUBOID(20)=ICUBOID(22)
    JCUBOID(21)=ICUBOID(23)
    JCUBOID(22)=ICUBOID(24)
    JCUBOID(23)=ICUBOID(17)
    JCUBOID(24)=ICUBOID(18)
   ENDIF
   RETURN

END SUBROUTINE



SUBROUTINE U(ICUBOID,JCUBOID,LPRIME)
! PERFORM UP LAYER ROTATE BY 90 DEGREES
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!
! 9->11;10->12;11->13;12->14;13->15;14->16;15->9;16->10
! 43->27;42->26;41->25;27->3;26->2;25->1;3->19;2->18;1->17;19->43;18->42;17->41

   IMPLICIT NONE
   INTEGER(4)  :: ICUBOID(1:48)
   INTEGER(4)  :: JCUBOID(1:48)
   LOGICAL     :: LPRIME ! TRUE CLOCKWISE; FALSE COUNTERCLOCKWISE

   JCUBOID=ICUBOID

   IF (LPRIME) THEN
    JCUBOID(11)=ICUBOID( 9)
    JCUBOID(12)=ICUBOID(10)
    JCUBOID(13)=ICUBOID(11)
    JCUBOID(14)=ICUBOID(12)
    JCUBOID(15)=ICUBOID(13)
    JCUBOID(16)=ICUBOID(14)
    JCUBOID( 9)=ICUBOID(15)
    JCUBOID(10)=ICUBOID(16)
    JCUBOID(27)=ICUBOID(43)
    JCUBOID(26)=ICUBOID(42)
    JCUBOID(25)=ICUBOID(41)
    JCUBOID( 3)=ICUBOID(27)
    JCUBOID( 2)=ICUBOID(26)
    JCUBOID( 1)=ICUBOID(25)
    JCUBOID(19)=ICUBOID( 3)
    JCUBOID(18)=ICUBOID( 2)
    JCUBOID(17)=ICUBOID( 1)
    JCUBOID(43)=ICUBOID(19)
    JCUBOID(42)=ICUBOID(18)
    JCUBOID(41)=ICUBOID(17)
   ELSE
    JCUBOID( 9)=ICUBOID(11)
    JCUBOID(10)=ICUBOID(12)
    JCUBOID(11)=ICUBOID(13)
    JCUBOID(12)=ICUBOID(14)
    JCUBOID(13)=ICUBOID(15)
    JCUBOID(14)=ICUBOID(16)
    JCUBOID(15)=ICUBOID( 9)
    JCUBOID(16)=ICUBOID(10)
    JCUBOID(43)=ICUBOID(27)
    JCUBOID(42)=ICUBOID(26)
    JCUBOID(41)=ICUBOID(25)
    JCUBOID(27)=ICUBOID( 3)
    JCUBOID(26)=ICUBOID( 2)
    JCUBOID(25)=ICUBOID( 1)
    JCUBOID( 3)=ICUBOID(19)
    JCUBOID( 2)=ICUBOID(18)
    JCUBOID( 1)=ICUBOID(17)
    JCUBOID(19)=ICUBOID(43)
    JCUBOID(18)=ICUBOID(42)
    JCUBOID(17)=ICUBOID(41)
   ENDIF
   RETURN

END SUBROUTINE



SUBROUTINE D(ICUBOID,JCUBOID,LPRIME)
! PERFORM DOWN LAYER ROTATE BY 90 DEGREES
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!
! 33->35;34->36;35->37;36->38;37->39;38->40;39->33;40->34
! 7->31;6->30;5->29;31->47;30->46;29->45;47->23;46->22;45->21;23->7;22->6;21->5

   IMPLICIT NONE
   INTEGER(4)  :: ICUBOID(1:48)
   INTEGER(4)  :: JCUBOID(1:48)
   LOGICAL     :: LPRIME ! TRUE CLOCKWISE; FALSE COUNTERCLOCKWISE

   JCUBOID=ICUBOID

   IF (LPRIME) THEN
    JCUBOID(35)=ICUBOID(33)
    JCUBOID(36)=ICUBOID(34)
    JCUBOID(37)=ICUBOID(35)
    JCUBOID(38)=ICUBOID(36)
    JCUBOID(39)=ICUBOID(37)
    JCUBOID(40)=ICUBOID(38)
    JCUBOID(33)=ICUBOID(39)
    JCUBOID(34)=ICUBOID(40)
    JCUBOID(31)=ICUBOID( 7)
    JCUBOID(30)=ICUBOID( 6)
    JCUBOID(29)=ICUBOID( 5)
    JCUBOID(47)=ICUBOID(31)
    JCUBOID(46)=ICUBOID(30)
    JCUBOID(45)=ICUBOID(29)
    JCUBOID(23)=ICUBOID(47)
    JCUBOID(22)=ICUBOID(46)
    JCUBOID(21)=ICUBOID(45)
    JCUBOID( 7)=ICUBOID(23)
    JCUBOID( 6)=ICUBOID(22)
    JCUBOID( 5)=ICUBOID(21)
   ELSE
    JCUBOID(33)=ICUBOID(35)
    JCUBOID(34)=ICUBOID(36)
    JCUBOID(35)=ICUBOID(37)
    JCUBOID(36)=ICUBOID(38)
    JCUBOID(37)=ICUBOID(39)
    JCUBOID(38)=ICUBOID(40)
    JCUBOID(39)=ICUBOID(33)
    JCUBOID(40)=ICUBOID(34)
    JCUBOID( 7)=ICUBOID(31)
    JCUBOID( 6)=ICUBOID(30)
    JCUBOID( 5)=ICUBOID(29)
    JCUBOID(31)=ICUBOID(47)
    JCUBOID(30)=ICUBOID(46)
    JCUBOID(29)=ICUBOID(45)
    JCUBOID(47)=ICUBOID(23)
    JCUBOID(46)=ICUBOID(22)
    JCUBOID(45)=ICUBOID(21)
    JCUBOID(23)=ICUBOID( 7)
    JCUBOID(22)=ICUBOID( 6)
    JCUBOID(21)=ICUBOID( 5)
   ENDIF
   RETURN

END SUBROUTINE



SUBROUTINE F(ICUBOID,JCUBOID,LPRIME)
! PERFORM FRONT LAYER ROTATE BY 90 DEGREES
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!
! 1->3;2->4;3->5;4->6;5->7;6->8;7->1;8->2
! 15->25;14->32;13->31;25->35;32->34;31->33;35->21;34->20;33->19;21->15;20->14;19->13

   IMPLICIT NONE
   INTEGER(4)  :: ICUBOID(1:48)
   INTEGER(4)  :: JCUBOID(1:48)
   LOGICAL     :: LPRIME ! TRUE CLOCKWISE; FALSE COUNTERCLOCKWISE

   JCUBOID=ICUBOID

   IF (LPRIME) THEN
    JCUBOID( 3)=ICUBOID( 1)
    JCUBOID( 4)=ICUBOID( 2)
    JCUBOID( 5)=ICUBOID( 3)
    JCUBOID( 6)=ICUBOID( 4)
    JCUBOID( 7)=ICUBOID( 5)
    JCUBOID( 8)=ICUBOID( 6)
    JCUBOID( 1)=ICUBOID( 7)
    JCUBOID( 2)=ICUBOID( 8)
    JCUBOID(25)=ICUBOID(15)
    JCUBOID(32)=ICUBOID(14)
    JCUBOID(31)=ICUBOID(13)
    JCUBOID(35)=ICUBOID(25)
    JCUBOID(34)=ICUBOID(32)
    JCUBOID(33)=ICUBOID(31)
    JCUBOID(21)=ICUBOID(35)
    JCUBOID(20)=ICUBOID(34)
    JCUBOID(19)=ICUBOID(33)
    JCUBOID(15)=ICUBOID(21)
    JCUBOID(14)=ICUBOID(20)
    JCUBOID(13)=ICUBOID(19)
   ELSE
    JCUBOID( 1)=ICUBOID( 3)
    JCUBOID( 2)=ICUBOID( 4)
    JCUBOID( 3)=ICUBOID( 5)
    JCUBOID( 4)=ICUBOID( 6)
    JCUBOID( 5)=ICUBOID( 7)
    JCUBOID( 6)=ICUBOID( 8)
    JCUBOID( 7)=ICUBOID( 1)
    JCUBOID( 8)=ICUBOID( 2)
    JCUBOID(15)=ICUBOID(25)
    JCUBOID(14)=ICUBOID(32)
    JCUBOID(13)=ICUBOID(31)
    JCUBOID(25)=ICUBOID(35)
    JCUBOID(32)=ICUBOID(34)
    JCUBOID(31)=ICUBOID(33)
    JCUBOID(35)=ICUBOID(21)
    JCUBOID(34)=ICUBOID(20)
    JCUBOID(33)=ICUBOID(19)
    JCUBOID(21)=ICUBOID(15)
    JCUBOID(20)=ICUBOID(14)
    JCUBOID(19)=ICUBOID(13)
   ENDIF
   RETURN

END SUBROUTINE



SUBROUTINE B(ICUBOID,JCUBOID,LPRIME)
! PERFORM BACK LAYER ROTATE BY 90 DEGREES
!
!           9 10 11
!          16  U 12
!          15 14 13
!
! 17 18 19  1  2  3 25 26 27 41 42 43
! 24  L 20  8  F  4 32  R 28 48  B 44
! 23 22 21  7  6  5 31 30 29 47 46 45
!
!          33 34 35
!          40  D 36
!          39 38 37
!
! 41->43;42->44;43->45;44->46;45->47;46->48;47->41;48->42
! 11->17;10->24;9->23;17->39;24->38;23->37;39->29;38->28;37->27;29->11;28->10;27->9

   IMPLICIT NONE
   INTEGER(4)  :: ICUBOID(1:48)
   INTEGER(4)  :: JCUBOID(1:48)
   LOGICAL     :: LPRIME ! TRUE CLOCKWISE; FALSE COUNTERCLOCKWISE

   JCUBOID=ICUBOID

   IF (LPRIME) THEN
    JCUBOID(43)=ICUBOID(41)
    JCUBOID(44)=ICUBOID(42)
    JCUBOID(45)=ICUBOID(43)
    JCUBOID(46)=ICUBOID(44)
    JCUBOID(47)=ICUBOID(45)
    JCUBOID(48)=ICUBOID(46)
    JCUBOID(41)=ICUBOID(47)
    JCUBOID(42)=ICUBOID(48)
    JCUBOID(17)=ICUBOID(11)
    JCUBOID(24)=ICUBOID(10)
    JCUBOID(23)=ICUBOID( 9)
    JCUBOID(39)=ICUBOID(17)
    JCUBOID(38)=ICUBOID(24)
    JCUBOID(37)=ICUBOID(23)
    JCUBOID(29)=ICUBOID(39)
    JCUBOID(28)=ICUBOID(38)
    JCUBOID(27)=ICUBOID(37)
    JCUBOID(11)=ICUBOID(29)
    JCUBOID(10)=ICUBOID(28)
    JCUBOID( 9)=ICUBOID(27)
   ELSE
    JCUBOID(41)=ICUBOID(43)
    JCUBOID(42)=ICUBOID(44)
    JCUBOID(43)=ICUBOID(45)
    JCUBOID(44)=ICUBOID(46)
    JCUBOID(45)=ICUBOID(47)
    JCUBOID(46)=ICUBOID(48)
    JCUBOID(47)=ICUBOID(41)
    JCUBOID(48)=ICUBOID(42)
    JCUBOID(11)=ICUBOID(17)
    JCUBOID(10)=ICUBOID(24)
    JCUBOID( 9)=ICUBOID(23)
    JCUBOID(17)=ICUBOID(39)
    JCUBOID(24)=ICUBOID(38)
    JCUBOID(23)=ICUBOID(37)
    JCUBOID(39)=ICUBOID(29)
    JCUBOID(38)=ICUBOID(28)
    JCUBOID(37)=ICUBOID(27)
    JCUBOID(29)=ICUBOID(11)
    JCUBOID(28)=ICUBOID(10)
    JCUBOID(27)=ICUBOID( 9)
   ENDIF
   RETURN

END SUBROUTINE



INTEGER FUNCTION IEXIST(CONFIG)
! RETURN TRUE IF CONFIG EXISTS IN THE LIST

   USE RUBIK3

   INTEGER(4)  :: CONFIG(1:48)
   INTEGER(4)  :: I,J,K

   ! DEBUG
   IF (ILIST < 0) THEN
    CALL CONFIGPRINT(CONFIG)
   ENDIF

   IEXIST=0
   IF (ILIST==0) RETURN
   DO I=1,ILIST
    K=1
    DO J=1,48
     IF (CONFIG(J)/=LIST(J,I)) THEN
      K=0
      EXIT
     ENDIF
    ENDDO
    IF (K==1) THEN
     IEXIST=I
     EXIT
    ENDIF
   ENDDO
   IF (ILIST < 0) THEN
    IF (IEXIST==0) WRITE(6,'(A)') 'NOT EXIST'
    IF (IEXIST/=0) WRITE(6,'(A,I3,X,48I1)') 'EXIST',I,(LIST(J,I),J=1,48)
   ENDIF

   RETURN

END FUNCTION



SUBROUTINE CONFIGPRINT(CONFIG)
! PRINT CONFIG FOR DEBUG

   USE RUBIK3

   INTEGER(4)  :: CONFIG(1:48)
   INTEGER(4)  :: I,J,K

   K=0
   DO J=1,48
    K=K+CONFIG(J)
   ENDDO
   WRITE(6,'(A,48I1,I5)') 'CONFIG  =',(CONFIG(J),J=1,48),K
   WRITE(6,*) 'LIST=',ILIST
   DO I=1,ILIST
    WRITE(6,'(I3,X,48I1)') I,(LIST(J,I),J=1,48)
   ENDDO

   RETURN

END SUBROUTINE

